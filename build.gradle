plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

description = 'Spring Cloud 微服务开发框架'

ext {
    javaVersion = JavaVersion.VERSION_11
    library = [
        springBootVersion = "2.3.2.RELEASE",
        springCloudVersion = "Hoxton.SR6",
        springCloudAlibabaVersion = "2.2.1.RELEASE",

        fastjsonVersion = "1.2.72",
        mybatisPlusVersion = "3.3.1",
        aliyunOssVersion = "3.10.2",
        qcloudCosVersion = "5.6.28"
    ]
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'cn.devifish.cloud'
    version = '1.0.0'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }

        dependencies {
            dependency "com.alibaba:fastjson:${fastjsonVersion}"
            dependency "com.aliyun.oss:aliyun-sdk-oss:${aliyunOssVersion}"

            dependencySet(group: "com.baomidou", version: "${mybatisPlusVersion}") {
                entry 'mybatis-plus-core'
                entry 'mybatis-plus-annotation'
                entry 'mybatis-plus-boot-starter'
            }

            dependency "com.qcloud:cos_api:${qcloudCosVersion}"
        }
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    compileJava.dependsOn(processResources)
}
