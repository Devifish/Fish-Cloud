plugins {
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "1.0.10.RELEASE" apply false
}

description = 'Spring Cloud 微服务开发框架'

allprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'cn.devifish.cloud'
    version = '1.0.0'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }

        dependencies {
            dependency "com.alibaba:fastjson:${fastjsonVersion}"
            dependency "com.aliyun:aliyun-java-sdk-core:${aliyunSdkCoreVersion}"
            dependency "com.aliyun.oss:aliyun-sdk-oss:${aliyunOssVersion}"

            dependencySet(group: "com.baomidou", version: "${mybatisPlusVersion}") {
                entry 'mybatis-plus-core'
                entry 'mybatis-plus-annotation'
                entry 'mybatis-plus-boot-starter'
            }

            dependency "com.qcloud:cos_api:${qcloudCosVersion}"
        }
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    compileJava.dependsOn(processResources)
}
